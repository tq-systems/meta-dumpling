require conf/distro/dumpling.conf

DISTRO = "dumpling-rzg2"
DISTRO_NAME = "Dumpling (TQ-Systems Dumpling Distribution) for TQ-Systems RZG2 BSP"

DISTRO_FEATURES_append = " ${DISTRO_FEATURES_LIBC}"

#
# Options below this line are imported from meta-rzg2/docs/sample/conf/*/poky-gcc/local.conf
# and not modified.
#

# add the static lib to SDK toolchain
SDKIMAGE_FEATURES_append = " staticdev-pkgs"

# for gstreamer omx plugins
LICENSE_FLAGS_WHITELIST = "commercial"

# Disable GPLv3 and GPLv3+ Software.
# This option should be used with meta-gplv2 which can help Yocto to search for old
# versions which support GPLv2
INCOMPATIBLE_LICENSE = "GPLv3 GPLv3+"

# for mmp test program
DISTRO_FEATURES_append = " mm-test"

# for weston v4l2 renderer
#DISTRO_FEATURES_append = " v4l2-renderer"

# OMX H264 decoder library for Linux (RTM0AC0000XV264D30SL41C)
DISTRO_FEATURES_append = " h264dec_lib"

# OMX H264 encoder library for Linux (RTM0AC0000XV264E30SL41C)
DISTRO_FEATURES_append = " h264enc_lib"

# OMX H265 decoder library for Linux (RTM0AC0000XV265D30SL41C)
DISTRO_FEATURES_append = " h265dec_lib"

# for meta-virtualization layer
DISTRO_FEATURES_append = "${@base_conditional('VIRTUALIZATION_CHECK', 'True', ' virtualization', '', d)}"

# Evaluation packages
#DISTRO_FEATURES_append = " use_eva_pkg"

# Configuration for ivi-shell and ivi-extension
#DISTRO_FEATURES_append = " ivi-shell"

# Switch among build setting Buster-full, Buster-limited and Jessie
#  - Buster-full (default)            : build all supported Debian 10 Buster recipes
#  - Buster-limited                   : build Debian 10 Buster, but only limited pakages similar to Jessie (4 main recipes)
#  - Jessie                           : build Debian 8 Jessie packages (only 4 main recipes)
#  - Not set (or different with above): not use CIP Core, use default packages version in Yocto
CIP_MODE = "Buster-full"
### Adjust environment according to CIP_MODE selection. Don't change manually, only change CIP_MODE above ###
USE_CIP_CORE = "${@'cipcore' if '${CIP_MODE}' in ['Buster-full', 'Buster-limited', 'Jessie'] else 'not-cipcore'}"
OVERRIDES .= ":${USE_CIP_CORE}"
BINUVERSION_cipcore = "${@'2.31.1' if 'Buster' in '${CIP_MODE}' else '2.25'}"
PREFERRED_VERSION_nativesdk-binutils_cipcore = "${BINUVERSION}"
GLIBCVERSION_cipcore = "${@'2.28' if 'Buster' in '${CIP_MODE}' else '2.19'}"
PREFERRED_VERSION_nativesdk-glibc-locale_cipcore = "${GLIBCVERSION}"
BBMASK_append_cipcore = "|glibc-mtrace"
RDEPENDS_${PN}_remove_pn-packagegroup-core-tools-debug_cipcore = "libc-mtrace"
PREFERRED_VERSION_openssl_forcevariable_cipcore = "${@'1.1.%' if 'Buster' in '${CIP_MODE}' else '1.0.%'}"
PREFERRED_VERSION_openssl-native_forcevariable_cipcore = "${@'1.1.%' if 'Buster' in '${CIP_MODE}' else '1.0.%'}"
PREFERRED_VERSION_nativesdk-openssl_forcevariable_cipcore = "${@'1.1.%' if 'Buster' in '${CIP_MODE}' else '1.0.%'}"
DEPENDS_remove_cipcore_pn-openssh = "openssl10"
DEPENDS_append_cipcore_pn-openssh = " openssl"
# Exclude perl_debian in Buster-full to allow building without GPLv3
BBMASK_append_cipcore = "|perl_debian"
# Exclude new version of nodejs, libgit2 in Jessie due to incompatibility with openssl version
BBMASK_append_cipcore = "${@'|nodejs_10.15.3|libgit2_0.27.9' if '${CIP_MODE}' not in ['Buster-full', 'Buster-limited'] else ''}"
# Exclude all related recipes if user chooses to not build CIP CORE
BBMASK_append_not-cipcore = "|recipes-cip-core"

# Enable multilib to support 32-bits applications.
require conf/multilib.conf
MULTILIBS = "multilib:lib32"
DEFAULTTUNE_virtclass-multilib-lib32 = "armv7vethf-neon-vfpv4"
USE_32BIT_PKGS = "1"

# Downgrade version for obexftp and openobex
PREFERRED_VERSION_obexftp = "0.23"
PREFERRED_VERSION_openobex = "1.5"

# Remove dnf package which causes high CPU workload
PACKAGE_INSTALL_remove = " dnf "

# Disable optee in meta-linaro layer
BBMASK = "meta-linaro/meta-optee/recipes-security/optee"

# for non-qt building
BBMASK .= "${@'|recipes-qt/|core-image-qt*' if 'qt5-layer' not in BBFILE_COLLECTIONS.split() else ''}"
